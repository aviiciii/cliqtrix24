// --------XX DEFAULT CODE XX--------
// Constants
BASEURL = "https://api.vercel.com/";
userFirstName = user.get("first_name");
userEmail = user.get("email");
userid = user.get("id");
response = Map();

// Get API KEY
query_map = Map();
query_map.put("criteria","userid==" + userid);
response_map = zoho.cliq.getRecords("verceldb",query_map);
if(response_map.get("status").equalsIgnoreCase("SUCCESS") && response_map.get("list").size() > 0)
{
	ACCESS_TOKEN = response_map.get("list").get(0).get("apikey");
	headers = Map();
	headers.put("Authorization","Bearer " + ACCESS_TOKEN);
}
else{
	form = {"type":"form","title":"Vercel API Key","name":"apikey","hint":"Add your API Key from Vercel to use this extension","button_label":"Submit","inputs":{{"name":"apikey","label":"API Key","placeholder":"Enter your Vercel API Key here","min_length":"0","max_length":"36","mandatory":true,"type":"text"}},"action":{"type":"invoke.function","name":"saveApiKey"}};
	return form;
}
// --------XX DEFAULT CODE XX--------


// Testing ----------------------------------------
if (message.containsIgnoreCase("getapi")){
	query_map = Map();
	criteria_string = "userid==" + userid;
	query_map.put("criteria",criteria_string);
	response_map = zoho.cliq.getRecords("verceldb",query_map);
	if(response_map.get("status").equalsIgnoreCase("SUCCESS") && response_map.get("list").size() > 0)
	{
		apikey = response_map.get("list").get(0).get("apikey");
		response.put("text", apikey);
	}
}
// Save API ---------------------------------------
else if(message.containsIgnoreCase("saveapi"))
{
	form = {"type":"form","title":"Vercel API Key","name":"apikey","hint":"Add your API Key from Vercel to use this extension","button_label":"Submit","inputs":{{"name":"apikey","label":"API Key","placeholder":"Enter your Vercel API Key here","min_length":"0","max_length":"36","mandatory":true,"type":"text"}},"action":{"type":"invoke.function","name":"saveApiKey"}};
	return form;
}


// Invite Member ----------------------------------
else if (message.containsIgnoreCase("invite member")){
	team_id = "team_Db2XQGTUtAc8mAd56BvgkWGR";
	return {
		"type": "form",
		"title": "Invite User",
		"name": "team-invite",
		"hint": "Invite a User to your Vercel Team",
		"button_label": "Add",
		"inputs": [
			{
		"name": "team_id",
		"value": team_id,
		"type": "hidden"
		},
		{
		"name": "email",
		"label": "Email",
		"placeholder": "user@example.com",
		"min_length": "0",
		"max_length": "25",
		"mandatory": true,
		"type": "text",
		"format": "email"
		},
		{
		"name": "role",
		"label": "Select Role",
		"placeholder": "Choose from the list of options",
		"max_selections": "1",
		"multiple": false,
		"mandatory": true,
		"type": "dynamic_select",
		"options": [
		{
		"value": "OWNER",
		"label": "Team Owner"
		},
		{
		"value": "MEMBER",
		"label": "Team Member"
		},
		{
		"value": "VIEWER",
		"label": "Viewer"
		},
		{
		"value": "DEVELOPER",
		"label": "Project Developer"
		},
		{
		"value": "BILLING",
		"label": "Billing Manager"
		},
		{
		"value": "CONTRIBUTOR",
		"label": "Contributor"
		}
		]
		}
		],
		"action": {
		"type": "invoke.function",
		"name": "inviteUser"
		}
	};

}


// View Teams ------------------------------------
else if(message.containsIgnoreCase("list teams") || message.containsIgnoreCase("my teams") || message.containsIgnoreCase("all teams"))
{
	text = "Select a team";
	// Get Teams
	endpointUrl = BASEURL + "v2/teams";
	
	res = invokeurl
	[
		url :endpointUrl
		type :GET
		headers:headers

	];
	teams = toJSONList(res.get("teams"));
	// Create buttons
	response = Map();
	response.put("text","The Teams that your are part of in Vercel");
	bot = Map();
	bot.put("name","Vercel");
	response.put("bot",bot);
	card = Map();
	card.put("title","Team Details");
	card.put("theme","modern-inline");
	response.put("card",card);
	slidesList = list();
	slidesList0 = Map();
	slidesList0.put("type","table");
	slidesList0.put("title","Your Teams");
	data = Map();
	headersList = list();
	headersList.add("Name");
	headersList.add("Role");
	headersList.add("Details");
	data.put("headers",headersList);
	rowsList = list();
	for each  team in teams
	{
		rowsList0 = Map();
		rowsList0.put("Name",team.get("name"));
		rowsList0.put("Role",team.get("membership").get("role"));
		rowsList0.put("Details","[View more](invoke.function|teamDetails|" + userEmail + "|" + team.get("id") + ")");
		rowsList.add(rowsList0);
	}
	data.put("rows",rowsList);
	slidesList0.put("data",data);
	slidesList.add(slidesList0);
	response.put("slides",slidesList);
	return response;
}
// Hello Handler -------------------------------------------------------
else if(message.containsIgnoreCase("HI") || message.containsIgnoreCase("HEY"))
{
	text = "Hi! How you doin'? :wink:";
	response.put("text",text);
	response.put("suggestions",
		{"list":{
			{"text":"View Projects"},
			{"text":"List Deployments"},
			{"text":"My Teams"},
			{"text":"Change Team"}
		}});
}
// View Projects -------------------------------------------------------
else if(message.containsIgnoreCase("View Projects"))
{
	
	// query data
	query_map = Map();
	criteria_string = "userid==" + userid;
	query_map.put("criteria",criteria_string);
	response_map = zoho.cliq.getRecords("verceldb",query_map);
	record_id = 0;
	// get team id	
	team_id = null;
	if(response_map.get("status").equalsIgnoreCase("SUCCESS") && response_map.get("list").size() > 0)
	{
		team_id = response_map.get("list").get(0).get("teamid");
		if(team_id == "personal")
		{
			team_id = null;
		}
	}
	// 	Get Projects
	endpointUrl = BASEURL + "v9/projects";
	if(team_id != null)
	{
		endpointUrl = endpointUrl + "?teamId=" + team_id;
	}
	
	res = invokeurl
	[
		url :endpointUrl
		type :GET
		headers:headers

	];
	projects = toJSONList(res.get("projects"));
	rows = Collection();
	for each  project in projects
	{
		// check if project is deployed
		if(project.get("targets").get("production").get("alias") == null)
		{
			// if deployed then add to deployed list
			row = {"name":project.get("name"),"owner":project.get("link").get("org"),"id":project.get("id"),"repo":project.get("link").get("org") + "/" + project.get("link").get("repo"),"prod":"N/A","url":"https://github.com/" + project.get("link").get("org") + "/" + project.get("link").get("repo")};
		}
		// if deployed then add to deployed list
		else
		{
			row = {"name":project.get("name"),"owner":project.get("link").get("org"),"id":project.get("id"),"repo":project.get("link").get("org") + "/" + project.get("link").get("repo"),"prod":project.get("targets").get("production").get("url"),"url":"https://github.com/" + project.get("link").get("org") + "/" + project.get("link").get("repo")};
			rows.add(row);
		}
	}
	// find if project is deployed
	no_of_deployed = 0;
	for each  row in rows
	{
		if(row.get("prod") != "N/A")
		{
			no_of_deployed = no_of_deployed + 1;
		}
	}
	// TABLE
	response = Map();
	response.put("text","Hey, " + userFirstName + "!");
	bot = Map();
	bot.put("name","Vercel");
	response.put("bot",bot);
	card = Map();
	card.put("title","Welcome to project section");
	card.put("theme","modern-inline");
	response.put("card",card);
	slidesList = list();
	slidesList0 = Map();
	slidesList0.put("type","table");
	slidesList0.put("title","Your Projects");
	buttonsList = list();
	buttonsList0 = Map();
	buttonsList0.put("label","Open Vercel");
	action = Map();
	action.put("type","open.url");
	data = Map();
	data.put("web","https://vercel.com/dashboard");
	action.put("data",data);
	buttonsList0.put("action",action);
	buttonsList.add(buttonsList0);
	slidesList0.put("buttons",buttonsList);
	data = Map();
	headersList = list();
	headersList.add("Project");
	headersList.add("Owner");
	headersList.add("Deployment");
	headersList.add("Github");
	data.put("headers",headersList);
	rowsList = list();
	for each  row in rows
	{
		rowsList0 = Map();
		rowsList0.put("Project",row.get("name"));
		rowsList0.put("Owner",row.get("owner"));

		rowsList0.put("Deployment","[View](" + "https://" + row.get("prod") + ")");
		rowsList0.put("Github","[Open]("+row.get("url")+ ")");
		rowsList.add(rowsList0);
	}
	data.put("rows",rowsList);
	slidesList0.put("data",data);
	slidesList.add(slidesList0);
	slidesList1 = Map();
	slidesList1.put("type","text");
	slidesList1.put("title","Summary");
	slidesList1.put("data","Deployed: " + no_of_deployed + "\nTotal: " + no_of_deployed);
	slidesList.add(slidesList1);
	response.put("slides",slidesList);
	response.put("slides",slidesList);
}
// View Deployements ---------------------------------------------------
else if(message.containsIgnoreCase("List Deployments"))
{
	text = "Glad to hear that! :smile:";
	// 	Get Projects
	// query data
	query_map = Map();
	criteria_string = "userid==" + userid;
	query_map.put("criteria",criteria_string);
	response_map = zoho.cliq.getRecords("verceldb",query_map);
	record_id = 0;
	// get team id	
	team_id = null;
	if(response_map.get("status").equalsIgnoreCase("SUCCESS") && response_map.get("list").size() > 0)
	{
		team_id = response_map.get("list").get(0).get("teamid");
		if(team_id == "personal")
		{
			team_id = null;
		}
	}
	endpointUrl = BASEURL + "v6/deployments";
	if(team_id != null)
	{
		endpointUrl = endpointUrl + "?teamId=" + team_id;
	}
	res = invokeurl
	[
		url :endpointUrl
		type :GET
		headers:headers

	];
	deployments = toJSONList(res.get("deployments"));
	rows = Collection();
	for each  deployment in deployments
	{
		if(deployment.get("target") == null)
		{
			type = "PR Preview";
		}
		else
		{
			type = "Production";
		}
		// check if project is deployed
		if(deployment.get("inspectorUrl") == null)
		{
			row = {"name":deployment.get("name"),"type":type,"inspect":"N/A","url":deployment.get("url"),"id":deployment.get("uid")};
		}
		// if deployed then add to deployed list
		else
		{
			row = {"name":deployment.get("name"),"type":type,"inspect":deployment.get("inspectorUrl"),"url":deployment.get("url"),"id":deployment.get("uid")};
		}
		rows.add(row);
	}
	// TABLE
	response = Map();
	response.put("text","Hey, " + userFirstName + "!");
	bot = Map();
	bot.put("name","Vercel");
	response.put("bot",bot);
	card = Map();
	card.put("title","Deployments");
	card.put("theme","modern-inline");
	response.put("card",card);
	// create table
	slidesList = list();
	slidesList0 = Map();
	slidesList0.put("type","table");
	slidesList0.put("title","Your Deployments");
	data = Map();
	headersList = list();
	headersList.add("Project");
	headersList.add("Type");
	headersList.add("Preview");
	headersList.add("Details");
	headersList.add("Vercel");
	data.put("headers",headersList);
	rowsList = list();
	for each  row in rows
	{
		rowsList0 = Map();
		rowsList0.put("Project",row.get("name"));
		rowsList0.put("Type",row.get("type"));
		rowsList0.put("Preview","[View](https://" + row.get("url") + ")");
		temp = "[+Show](invoke.function|getdeployment|" + userEmail + "|" + row.get("id") + ")";
		rowsList0.put("Details",temp);
		
		rowsList0.put("Vercel","[Open](" + row.get("inspect") + ")");
		rowsList.add(rowsList0);
	}
	data.put("rows",rowsList);
	slidesList0.put("data",data);
	slidesList.add(slidesList0);
	response.put("slides",slidesList);
}
// Change Teams---------------------------------------------------------
else if(message.containsIgnoreCase("Change Team") || message.containsIgnoreCase("Change Teams") || message.containsIgnoreCase("Switch Team"))
{
	// Get Teams
	endpointUrl = BASEURL + "v2/teams";
	res = invokeurl
	[
		url :endpointUrl
		type :GET
		headers:headers

	];
	teams = toJSONList(res.get("teams"));
	options = list();
	options.add({"label":"Personal","value":"personal"});
	for each  team in teams
	{
		options.add({"label":team.get("name"),"value":team.get("id")});
	}
	inputs = list();
	inputs.add({"type":"select","trigger_on_change":true,"name":"teamid","label":"Select Team","hint":"Choose from your teams","placeholder":"Personal","mandatory":true,"options":options});
	form = {"type":"form","title":"Change Team","hint":"Select your Vercel Team.","name":"ID","version":1,"button_label":"Change","actions":{"submit":{"type":"invoke.function","name":"changeTeam"}},"inputs":inputs};
	return form;
}
// Something Else ------------------------------------------------------
else
{
	text = "Sorry, I didn't get that. ðŸ¤”";
	response.put("text",text);
	response.put("suggestions",
		{"list":{
			{"text":"View Projects"},
			{"text":"List Deployments"},
			{"text":"My Teams"},
			{"text":"Change Team"}
		}});
}
return response;
